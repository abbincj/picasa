buildscript {
  ext.kotlin_version = '1.0.5-2'
  ext.appengine_version = '1.9.46'
  ext.junit_version = '1.0.0-M3'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.google.appengine:gradle-appengine-plugin:$appengine_version"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_version"
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    url 'http://repository.jetbrains.com/all'
  }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'appengine'
apply plugin: 'org.junit.platform.gradle.plugin'

webAppDirName = 'web'

sourceSets {
  main {
    java { srcDir 'src' }
    resources { srcDir 'src' }
  }
  test {
    java { srcDir 'test' }
    resources { srcDir 'test' }
  }
}

dependencies {
  appengineSdk "com.google.appengine:appengine-java-sdk:$appengine_version"
  compile 'org.apache.velocity:velocity:1.7'
  compile 'com.atlassian.commonmark:commonmark:0.7.1'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
  testCompile 'org.jetbrains.spek:spek-api:1.1.0-beta2'
  testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.0-beta2'
  testCompile 'com.nhaarman:mockito-kotlin:0.11.0'
  testCompile 'org.assertj:assertj-core:3.6.1'
}

appengine {
  downloadSdk = true
  appcfg {
    oauth2 = true
  }
  jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']
}

junitPlatform {
  filters {
    engines {
      include 'spek'
    }
  }
}
